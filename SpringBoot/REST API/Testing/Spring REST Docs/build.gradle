plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
  // asciidoctor 플러그인 추가
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

// ext에 정의한 snippetsDir 사용
asciidoctor {
  attributes(
			"snippets": snippetsDir,
      // 생략 가능
			"output-format": "html"
	)
  // 이전 문서 삭제
  doFirst {
      delete file('build/docs/asciidoc')
  }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

// jar에 문서 넣기(배포에 포함)
tasks.named('bootJar') {
    dependsOn tasks.named('asciidoctor')
    from("build/docs/asciidoc") {
        into "static/docs"
    }
}
